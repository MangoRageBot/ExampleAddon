buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            url = "https://maven.minecraftforge.net/"
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
        classpath 'org.mangorage:MangoBotGradle:5.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'MangoBotGradle'
apply plugin: 'maven-publish'

group = 'org.mangorage'
version = '1.0.0'

println("Version: " + version)

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(22))
    }
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Required
    installer('org.mangorage:installer:4.0.3')
    bot('org.mangorage:mangobot:11.0.23')

    // Optional
    plugin('org.mangorage:mangobotplugin:12.0.18')

    // library('com.example:example-artifact:version') // Gets put into dependencies.json
    // embedLibrary('com.example:example-artifact:version') // Doesn't get put into dependencies.json

}

MangoBotConfig {
    setJarTask(jar)
}

shadowJar {
    configurations = [project.getConfigurations().findByName('embedLibrary')]
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mangobot_version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['example.plugin.json']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifact jar

        pom {
            name = 'MangoBot Example Addon Plugin'
            description = 'The Example Addon Template for MangoBot'
            url = 'https://github.com/MangoRageBot/ExampleAddon'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/ExampleAddon/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.minecraftforge.net/"

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}